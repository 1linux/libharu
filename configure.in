dnl this files has to be processed by autoconf
AC_PREREQ(2.60)

HPDF_MAJOR_VERSION=2
HPDF_MINOR_VERSION=1
HPDF_BUGFIX_VERSION=0
HPDF_EXTRA_VERSION="-dev"

HPDF_VERSION="${HPDF_MAJOR_VERSION}.${HPDF_MINOR_VERSION}.${HPDF_BUGFIX_VERSION}${HPDF_EXTRA_VERSION}"

AC_INIT([libhpdf],[2.1.0])
AC_CONFIG_SRCDIR([README])
AC_CONFIG_SRCDIR(include/hpdf.h)
AC_CONFIG_HEADERS(include/hpdf_config.h)
AM_INIT_AUTOMAKE([no-define])
AM_MAINTAINER_MODE

dnl Checks for programs.
AC_PROG_CC
AC_PROG_LD
AM_PROG_LIBTOOL
AC_PROG_INSTALL

dnl Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_SIZE_T

dnl Checks for header files.
AC_CHECK_HEADERS(string.h strings.h unistd.h stdint.h)

AC_CHECK_LIB([m], [floor], [LIBS="$LIBS -lm"], [AC_MSG_ERROR([can't continue without libm])])

DEFAULT_INSTALL_PREFIX="/usr/local"
STANDARD_PREFIXES="/usr /usr/local /opt /local"

dnl {{{ --enable-debug
AC_ARG_ENABLE(debug,
  [AS_HELP_STRING([--enable-debug],[enable debugging symbols and compile flags])
  ],
  [
    if test x"$enableval" = xyes ; then
      debug="yes"
    else
      debug="no"
    fi
  ]
)

if test x"$debug" = xyes ; then
  AC_DEFINE([HPDF_DEBUG], [], [debug build])

  if test x"$GCC" = xyes; then

    dnl Remove any optimization flags from CFLAGS
    changequote({,})
    CFLAGS=`echo "$CFLAGS" | /usr/bin/sed -e 's/-O[0-9s]*//g'`
    CFLAGS=`echo "$CFLAGS" | /usr/bin/sed -e 's/-g[0-2]\? //g'`
    changequote([,])
    CFLAGS="$CFLAGS -g3 -Wall -O0"

  fi

  dnl Do not strip symbols from developer object files.
  INSTALL_STRIP_FLAG=""
else
  dnl Make sure to strip symbols from non-developer object files.
  INSTALL_STRIP_FLAG="-s"
fi
dnl }}}

dnl {{{ --with-libdir
AC_ARG_WITH(libdir,
  [AS_HELP_STRING([--with-libdir],[look for libraries in .../NAME rather than .../lib])
  ],
  [LIBDIR=$with_libdir],
  [LIBDIR=lib]
)
dnl }}}

dnl {{{ --with-zlib
AC_ARG_WITH(zlib,
  [AS_HELP_STRING([--with-zlib],[specify Zlib install prefix])
  ],
  [ ],
  [with_zlib=yes]
)

if test "x$with_zlib" = "xno"; then
  AC_DEFINE([HPDF_NOZLIB], [], [zlib is not available])
else
  AC_MSG_CHECKING([Zlib install prefix])

  if test "x$with_zlib" = "xyes"; then
    for i in `echo "$STANDARD_PREFIXES"`; do
      if test -f "$i/include/zlib.h"; then
        ZLIB_DIR="$i"
        break;
      fi
    done
  else
    if test -f "$with_zlib/include/zlib.h"; then
      ZLIB_DIR="$with_zlib"
      break;
    else
      AC_MSG_ERROR([Can't find Zlib headers under $with_zlib directory]);
    fi
  fi

  if test "x$ZLIB_DIR" = "x"; then
    AC_MSG_ERROR([Unable to locate Zlib headers, please use --with-zlib=<DIR>]);
  fi

  AC_MSG_RESULT([$ZLIB_DIR])
  LDFLAGS="$LDFLAGS -L$ZLIB_DIR/$LIBDIR"
  CFLAGS="$CFLAGS -I$ZLIB_DIR/include"
  LIBS="$LIBS -lz"

  AC_CHECK_LIB([z], [deflate], [], [
    AC_MSG_ERROR([deflate() is missing, check config.log for more details])
  ])

  HAVE_ZLIB=yes
fi
dnl }}}

dnl {{{ --with-png
AC_ARG_WITH(png,
  [AS_HELP_STRING([--with-png],[specify libpng install prefix])
  ],
  [ ],
  [with_png=yes]
)

if test "x$with_png" = "xno"; then
  AC_DEFINE([HPDF_NOPNGLIB], [], [libpng is not available])
else

  if test "x$HAVE_ZLIB" != "xyes"; then
    AC_MSG_ERROR([PNG support requires Zlib, but it's not enabled. Either enable Zlib or disable PNG support.]);
  fi

  AC_MSG_CHECKING([libpng install prefix])

  if test "x$with_png" = "xyes"; then
    for i in `echo "$STANDARD_PREFIXES"`; do
      if test -f "$i/include/png.h"; then
        LIBPNG_DIR="$i"
        break;
      fi
    done
  else
    if test -f "$with_png/include/png.h"; then
      LIBPNG_DIR="$with_png"
      break;
    else
      AC_MSG_ERROR([Can't find libpng headers under $with_png directory]);
    fi
  fi

  if test "x$LIBPNG_DIR" = "x"; then
    AC_MSG_ERROR([Unable to locate libpng headers, please use --with-png=<DIR>]);
  fi

  AC_MSG_RESULT([$LIBPNG_DIR])
  LDFLAGS="$LDFLAGS -L$LIBPNG_DIR/$LIBDIR"
  CFLAGS="$CFLAGS -I$LIBPNG_DIR/include"
  LIBS="$LIBS -lpng"

  AC_CHECK_LIB([png], [png_init_io], [], [
    AC_MSG_ERROR([png_init_io() is missing, check config.log for more details])
  ])
fi
dnl }}}

dnl link against libm
LIBS="$LIBS -lm"
AC_SUBST(INSTALL_STRIP_FLAG)

AC_CONFIG_FILES([Makefile src/Makefile include/Makefile])
AC_OUTPUT

# vim600: ts=2 sw=2 et
